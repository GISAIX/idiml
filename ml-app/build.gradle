plugins {
  // enable the ScalaTest test runner
  id "com.github.maiflai.scalatest" version "0.10"
  // Test coverage
  id "org.scoverage" version "2.0.0"
}

dependencies {
  compile project(':predict')
  compile project(':train')
  compile "org.apache.commons:commons-csv:1.2"
  runtime "org.scala-lang:scala-compiler:${project.ext.scalaVersion}.${project.ext.scalaCompilerBuild}"
  runtime "org.slf4j:slf4j-simple:1.7.13"
  scoverage "org.scoverage:scalac-scoverage-plugin_${project.ext.scalaVersion}:1.1.0", "org.scoverage:scalac-scoverage-runtime_${project.ext.scalaVersion}:1.1.0"
}

description = "Idibon ML CLI Utilities and Application Libraries"
apply plugin: 'application'
mainClassName = 'com.idibon.ml.app.App'

/* the "application" plugin builds distribution ZIP and JAR files by
 * default. we don't want to publish these, since that adds ~140M to
 * every maven deploy with transient dependencies that don't change.
 */
configurations.archives.artifacts
  .findAll { it.buildDependencies.getDependencies(uploadArchives)
               .any { it.name == "distZip" || it.name == "distTar" } }
  .each { configurations.archives.artifacts.remove(it) }
