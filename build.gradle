// Common configuration for all subprojects listed in settings.gradle
apply plugin: 'idea'

subprojects {

  if (project.name.equals("cld2")) {
    // CLD2 uses no common configuration properties.
    return
  }

  version = "0.0.1-SNAPSHOT"

  apply plugin: 'scala'

  // Java 8
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"

  ext {
    scalaVersion = '2.11'
    scalaCompilerBuild = '7'
    sparkVersion = '1.5.2'
    slf4jVersion = '1.7.13'
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    // Use Scala version 2.11
    compile "org.scala-lang:scala-library:${project.ext.scalaVersion}.${project.ext.scalaCompilerBuild}"

    // Common Spark / Spark MLLib dependencies
    compile("org.apache.spark:spark-core_${project.ext.scalaVersion}:${project.ext.sparkVersion}") {
      // Spark (and its transitive dependencies) aren't the best slf4j
      // citizens; they should only include slf4j-api, but some library
      // is also including log4j and the slf4j minimal-log4j implementation.
      // exclude these dependencies so that logging backends don't conflict
      exclude group: "org.slf4j", module: "slf4j-log4j12"
      exclude group: "log4j"
    }

    compile("org.apache.spark:spark-mllib_${project.ext.scalaVersion}:${project.ext.sparkVersion}") {
      // see comment above about excluding these dependencies
      exclude group: "org.slf4j", module: "slf4j-log4j12"
      exclude group: "log4j"
    }

    // Json 4 Scala (Json4s)
    compile group: 'org.json4s', name: "json4s-native_${project.ext.scalaVersion}", version: '3.3.0'

    // Scala logging (https://github.com/typesafehub/scala-logging),
    compile("com.typesafe.scala-logging:scala-logging_${project.ext.scalaVersion}:3.1.0")

    // SLF4J (http://www.slf4j.org)
    compile "org.slf4j:slf4j-api:${project.ext.slf4jVersion}"

    // ScalaTest framework
    testCompile "org.scalatest:scalatest_${project.ext.scalaVersion}:2+"

    // mockito framework
    testCompile 'org.mockito:mockito-core:1.10+'

    // Generates an HTML report for tests in ../build/reports/tests/index.html
    testRuntime 'org.pegdown:pegdown:1.1.0'

    // use simple slf4j bindings for testing
    testRuntime "org.slf4j:slf4j-simple:${project.ext.slf4jVersion}"
  }
}
