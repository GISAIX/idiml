// Common configuration for all subprojects listed in settings.gradle
apply plugin: 'idea'

subprojects {
  version = "0.0.1-SNAPSHOT"

  apply plugin: 'scala'

  // Java 8
  sourceCompatibility = "1.8"
  targetCompatibility = "1.8"

  ext {
    scalaVersion = '2.11'
    scalaCompilerBuild = '7'
    sparkVersion = '1.5.2'
    log4jVersion = '2.5'
  }

  repositories {
    mavenCentral()
  }

  configurations {
    // exclude the log4j 1.2-compatible bindings that come by default
    // with SLF4J (which comes with scala-logging). we use log4j 2
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }

  dependencies {
    // Use Scala version 2.11
    compile "org.scala-lang:scala-library:${project.ext.scalaVersion}.${project.ext.scalaCompilerBuild}"

    // Common Spark / Spark MLLib dependencies
    compile group: 'org.apache.spark', name: "spark-core_${project.ext.scalaVersion}", version: project.ext.sparkVersion
    compile group: 'org.apache.spark', name: "spark-mllib_${project.ext.scalaVersion}", version: project.ext.sparkVersion

    // Json 4 Scala (Json4s)
    compile group: 'org.json4s', name: "json4s-native_${project.ext.scalaVersion}", version: '3.3.0'

    // Scala logging (https://github.com/typesafehub/scala-logging),
    compile group: 'com.typesafe.scala-logging', name: "scala-logging_${project.ext.scalaVersion}", version: '3.1.0'

    // log4j2 + slf4j bindings
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: "${project.ext.log4jVersion}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: "${project.ext.log4jVersion}"
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: "${project.ext.log4jVersion}"

    // ScalaTest framework
    testCompile "org.scalatest:scalatest_${project.ext.scalaVersion}:2+"

    // mockito framework
    testCompile 'org.mockito:mockito-core:1.10+'

    // Generates an HTML report for tests in ../build/reports/tests/index.html
    testRuntime 'org.pegdown:pegdown:1.1.0'
  }
}
